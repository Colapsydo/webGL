<!DOCTYPE html>
<html >
  <head>
    <meta charset="UTF-8">
    <title>Directional Light - Per Fragment</title>
    
    
    
    
        <link rel="stylesheet" href="css/style.css">

    
    
    
  </head>

  <body>

    <script id="vertex-shader" type="x-shader/x-vertex">
		precision mediump float;
		attribute vec4 aPosition; 
		attribute vec3 aNormal; 
		
		uniform mat4 uModelMatrix; 
		uniform mat4 uViewMatrix; 
		uniform mat3 uNormalMatrix; 
		uniform mat4 uProjectionMatrix; 
		
		varying vec3 vNormal;
		varying vec3 vEyeDir;
	
		void main() { 
			vec4 vertPos4 = uViewMatrix*uModelMatrix*aPosition;
				//to use a transformed point we need to pass it from homogenous 4D to 3D
			vec3 vertPos = vec3(vertPos4.xyz/vertPos4.w);
			
			//vertex position
			gl_Position = uProjectionMatrix*uViewMatrix*uModelMatrix*aPosition;
			
			//passing Varying
				//Since normalization needed to be done on the fragment shader we can avoid doing it here
			vNormal = uNormalMatrix*aNormal;
			vEyeDir = -vertPos;
	}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
		precision mediump float;
	
		uniform mat4 uViewMatrix; 
		uniform vec4 uLightDir;
		uniform vec4 uAmbient;
		uniform vec4 uDiffuse;
		uniform vec4 uSpecular;
		uniform float uShininess;
		
		varying vec3 vNormal;
		varying vec3 vEyeDir;
		
		void main() {
			/*  In order to calculate the cos of the angle made by two vectors
					we can use the dot product of these vectors at the condition
					that both vectors are normalized */
			
			vec3 normal = normalize(vNormal);
			vec3 eyeDir = normalize(vEyeDir);
			vec3 light = normalize(uViewMatrix*uLightDir).xyz;
			
			//ambient
			vec3 ambient = uAmbient.rgb;
			
			//diffuse								
			float diffCoeff = max(dot(light,normal),0.0);
			vec3 diffuse = diffCoeff*uDiffuse.rgb;
	
			//specular
			vec3 specular;
			if(diffCoeff>0.0){
				vec3 halfway = normalize(eyeDir + light);
				float specCoeff = pow(max(dot(halfway, normal),0.0),uShininess);				
				specular = specCoeff*uSpecular.rgb;
			}else{
				specular = vec3(0.0,0.0,0.0);
			}
			
			gl_FragColor = vec4(ambient+diffuse+specular, 1.0 ); 
		}
</script>

<div id="container">
		<canvas id="gl-canvas" width="580" height="580">
				Oops ... your browser doesn't support the HTML5 canvas element
		</canvas>
</div>
    	<script src='../../lib/webgl-utils.js'></script>
		<script src='../../lib/initshaders.js'></script>
		<script src='../../lib/mv.js'></script>
        <script src="js/index.js"></script>

    
    
    
  </body>
</html>
