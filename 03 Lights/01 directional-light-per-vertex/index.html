<!DOCTYPE html>
<html >
  <head>
    <meta charset="UTF-8">
    <title>Directional Light - Per Vertex</title>
    
    
    
    
        <link rel="stylesheet" href="css/style.css">

    
    
    
  </head>

  <body>

    <script id="vertex-shader" type="x-shader/x-vertex">
		attribute vec4 aPosition; 
		attribute vec3 aNormal; 
		
		uniform mat4 uModelMatrix; 
		uniform mat4 uViewMatrix; 
		uniform mat3 uNormalMatrix; 
		uniform mat4 uProjectionMatrix; 
	
		uniform vec4 uAmbient;
		uniform vec4 uDiffuse;
		uniform vec4 uSpecular;
		uniform vec4 uLightDir;
		uniform float uShininess;
		
		varying vec3 vColor; 
	
		void main() { 
			/*  In order to calculate the cos of the angle made by two vectors
					we can use the dot product of these vectors at the condition
					that both vectors are normalized */
	
			vec3 tNormal = normalize(uNormalMatrix*aNormal);			
			
			/*	A position in world space gives a fixed directional light in camera space
	  			Multiply the direction by the viewMatrix allow to get a fixed light in world space */
			vec3 light = normalize(uViewMatrix*uLightDir).xyz;
			vec4 vertPos4 = uViewMatrix*uModelMatrix*aPosition;
			vec3 vertPos = vec3(vertPos4.xyz/vertPos4.w);
			
			//ambient
			vec3 ambient = uAmbient.rgb;
			
			//diffuse								
			float diffCoeff = max(dot(light,tNormal),0.0);
			vec3 diffuse = diffCoeff*uDiffuse.rgb;
	
			//specular
			vec3 specular;
			if(diffCoeff>0.0){
					//eyeDir is the vector from vertex to camera
				vec3 eyeDir = normalize(-vertPos);
				vec3 halfway = normalize(eyeDir + light);
				float specCoeff = pow(max(dot(halfway, tNormal),0.0),uShininess);				
				specular = specCoeff*uSpecular.rgb;
			}else{
				specular = vec3(0.0,0.0,0.0);
			}
			
	
			//vertex position
			gl_Position = uProjectionMatrix*uViewMatrix*uModelMatrix*aPosition;
			
			//passing Varying
			vColor = ambient + diffuse + specular; 
	}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
		precision mediump float; 
		varying vec3 vColor; 
		
		void main() { 
			gl_FragColor = vec4(vColor, 1.0 ); 
		}
</script>

<div id="container">
		<canvas id="gl-canvas" width="580" height="580">
				Oops ... your browser doesn't support the HTML5 canvas element
		</canvas>
</div>
    	<script src='../../lib/webgl-utils.js'></script>
		<script src='../../lib/initshaders.js'></script>
		<script src='../../lib/mv.js'></script>
        <script src="js/index.js"></script>

    
    
    
  </body>
</html>
